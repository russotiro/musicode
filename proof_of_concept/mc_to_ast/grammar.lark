%import common.WORD
start: statement+
LPAREN: "("
RPAREN: ")"
LBRACKET: "["
RBRACKET: "]"
statement: LPAREN (title | subtitle | composer | basic_info | instruments | part) RPAREN
// expressions cannot be statements
expression: LPAREN (notes | staff) RPAREN

title: "title" "\"" WORD+ "\""
subtitle: "subtitle" "\"" WORD+ "\""
composer: "composer" "\"" WORD+ "\""

measure: /m[1-9][0-9]*/

basic_info: clef | time | key | tempo

clef: "clef" clef_value measure? instrument?
clef_value: /(treble|bass)/

time: "time" time_signature measure?
time_signature: (/[1-9][0-9]*/ "/" duration) | /common/ | /cut/

key: "key" key_signature measure? instrument?
key_signature: /[A-G][#b]?[Mm]?/ //default to major in transpiler if no Mm given

tempo: "tempo" ((/\"/ WORD+ /\"/) | ("/" duration "=" /[1-9][0-9]*/) | (/\"/ WORD+ /\"/ "/" duration "=" /[1-9][0-9]*/)) measure?

instruments: "instruments" instrument+

part: "part" instrument instrument_name (staff+ | notes)
instrument: WORD
instrument_name: "\"" WORD+ "\""
staff: LPAREN "staff" notes RPAREN
notes: LPAREN "notes" event+ RPAREN
event: (clef_event | voices | tuplet | ((rest | ((note | chord) modifier_list?)) "/" duration)) | environment

// environments
environment: slur | pedal | crescendo | decrescendo
slur: regular_slur | phrasing_slur
regular_slur: (/\{/ event+ /\}/)
phrasing_slur: (/\\\{/ event+ /\\\}/)
pedal: LPAREN "pedal" event+ RPAREN
crescendo: LPAREN /cresc(endo)?(Text)?/ event+ RPAREN
decrescendo: LPAREN /((decresc(endo)?)|(dim(inuendo)?))(Text)?/ event+ RPAREN

// other stuff
clef_event: LPAREN "clef" clef_value RPAREN
voices: LPAREN "voices" notes+ RPAREN
tuplet: LPAREN "tuplet" /[1-9][0-9]*\/[1-9][0-9]*/ event+ RPAREN
note: pitch octave
pitch: /[A-Ga-g][#b]?/
octave: /[0-8]/
rest: ("r" | "R")
chord: note ("_" note)*
modifier_list: (LBRACKET modifier_keyword+ RBRACKET) | articulation_symbol+
modifier_keyword: articulation_keyword | /stemUp/ | /stemDown/ | /lift/ | dynamics_keyword
articulation_keyword: /staccato/ | /marcato/ | /tenuto/ | /accent/ | /fermata/ | /tie/ | /gliss(ando)?/
articulation_symbol: /\./ | /\^/ | /_/ | />/ | /~/
dynamics_keyword: /p{1,3}/ | /mp/ | /mf/ | /f{1,3}/ | /s[pf]/ | /sf{1,2}z/ | /fp/
duration: /[1248(16)(32)(64)(128)]\.{0,3}/

%ignore " "
%ignore "\n"