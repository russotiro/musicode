%import common.WORD
start: statement+
_LPAREN: "("
_RPAREN: ")"
_LBRACKET: "["
_RBRACKET: "]"
statement: _LPAREN (title | subtitle | composer | group | part) _RPAREN

title: "title" "\"" WORD+ "\""
subtitle: "subtitle" "\"" WORD+ "\""
composer: "composer" "\"" WORD+ "\""
group: "group" /begin|end/

// part construction
part: "part" instrument_name staff+
instrument_name: "\"" WORD+ "\""
staff: _LPAREN "staff" staff_environment+ _RPAREN

// staff environments
staff_environment: _LPAREN (notes | voices | ending | coda | staff_event) _RPAREN
notes: "notes" notes_argument+
notes_argument: note_environment | (_LPAREN staff_event _RPAREN) | note_event
voices: "voices" voice+
voice: _LPAREN "voice" (_LPAREN notes _RPAREN)+ _RPAREN
ending: "ending" /[1-9](,[1-9])*/ staff_environment+ // no more than 9 endings!!
coda: "coda" staff_environment+

// note environments
note_environment: _LPAREN (tuplet | grace | tremolo) _RPAREN
tuplet: "tuplet" /[1-9][0-9]*\/[1-9][0-9]*/ notes_argument+
grace: "grace" grace_type? _LPAREN notes _RPAREN (note_event | note_environment)
grace_type: /slash|noSlash/
tremolo: "tremolo" /[1-3]/ note_event~2

// staff events
staff_event: clef | key | time | tempo | barline | symbol | text | line_page_break
clef: "clef" WORD
key: "key" /[A-G][#b]?[Mm]?/
time: "time" ((/[1-9][0-9]*/ "/" duration) | /common/ | /cut/)

tempo: "tempo" (tempo_text | (tempo_text? tempo_number))
tempo_text: /\"/ WORD+ /\"/
tempo_number: "/" duration "=" /[1-9][0-9]*/

barline: "barline" WORD
symbol: "symbol" WORD
line_page_break: "break" WORD

text: road_map_text | expression_text | technique_text
road_map_text: /(D\.C\.|D\.S\.) al ([fF]ine|[cC]oda)/ | /toCoda/
expression_text: "expression" "\"" WORD+ "\""
technique_text: "technique" "\"" WORD+ "\""

// note events
note_event: (rest | ((note | chord) modifier_list?)) "/" duration
rest: ("r" | "R") (_LBRACKET beaming _RBRACKET)?
note: pitch octave
chord: note ("_" note)+
pitch: /[A-Ga-g][#b]?/
octave: /[0-8]/
duration: /(1(6|28)?|2|4|8|32|64)\.{0,3}/

// modifiers
modifier_list: ((_LBRACKET modifier_keyword+ _RBRACKET) | articulation_symbol)+
modifier_keyword: articulation_keyword | /stemUp|stemDown/ | piano_keyword | dynamics_keyword | beaming
articulation_keyword: /staccato|marcato|tenuto|accent|fermata|tie|gliss(ando)?|(phrasingSlur|slur)(Begin|End)|tremolo(8|16|32)|trill/
articulation_symbol: /\./ | /\^/ | /_/ | />/ | /~/
piano_keyword: /pedalBegin/ | /pedalEnd/ | /pedalLift/ | /pianoFinger[1-5]/
dynamics_keyword: /p{1,3}/ | /m[pf]/ | /f{1,3}/ | /s[pf]/ | /sfz/ | /fp/ | /cresc(endo)?(Text)?(Begin|End)/ | /(decresc(endo)?|dim(inuendo)?)(Text)?(Begin|End)/
beaming: /beam(None|Begin(Up|Down)?|End)|beamStem(Left|Right)Count[0-5]/

%ignore " "
%ignore "\n"